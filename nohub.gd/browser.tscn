[gd_scene load_steps=2 format=3 uid="uid://4n0eqw25becg"]

[sub_resource type="GDScript" id="GDScript_xkt7l"]
script/source = "extends Control

@onready var host_input: LineEdit = %\"Host Input\"
@onready var connect_button: Button = %\"Connect Button\"
@onready var disconnect_button: Button = %\"Disconnect Button\"
@onready var status_label: Label = %\"Status Label\"
@onready var lobby_list: ItemList = %\"Lobby List\"
@onready var create_name_input: LineEdit = %\"Lobby Name Input\"
@onready var create_button: Button = %\"Create Lobby Button\"
@onready var address_label: Label = %\"Address Label\"
@onready var selected_lobby_label: Label = %\"Selected Lobby Label\"
@onready var lock_lobby_button: Button = %\"Lock Lobby Button\"
@onready var hide_lobby_button: Button = %\"Hide Lobby Button\"
@onready var join_lobby_button: Button = %\"Join Lobby Button\"

@onready var modal: Popup = %Modal
@onready var modal_message_label: Label = %\"Message Label\"
@onready var modal_confirm_button: Button = %\"Confirm Button\"

var list_interval: float = 2.0
var time: float = 0.0

var _connection: StreamPeerTCP = null
var _client: NohubClient = null
var _last_list: float = -1.
var _lobbies_in_list: Array[NohubLobby] = []

var _selected_lobby: NohubLobby = null

func set_selected_lobby(lobby: NohubLobby) -> void:
	_selected_lobby = lobby
	if is_instance_valid(lobby):
		selected_lobby_label.text = lobby.id
		lock_lobby_button.text = \"Unlock\" if lobby.is_locked else \"Lock\"
		hide_lobby_button.text = \"Hide\" if lobby.is_visible else \"Publish\"
	else:
		selected_lobby_label.text = \"\"
		lock_lobby_button.text = \"Lock\"
		hide_lobby_button.text = \"Hide\"

func get_selected_lobby() -> NohubLobby:
	return _selected_lobby

func popup(text: String) -> void:
	modal_message_label.text = text
	modal.show()

func _ready():
	connect_button.pressed.connect(_connect)
	disconnect_button.pressed.connect(_disconnect)
	create_button.pressed.connect(_create_lobby)
	lobby_list.item_selected.connect(_select_lobby)

	join_lobby_button.pressed.connect(_join_lobby)
	lock_lobby_button.pressed.connect(_lock_lobby)
	hide_lobby_button.pressed.connect(_hide_lobby)
	
	modal_confirm_button.pressed.connect(func():
		modal.hide()
	)
	
	address_label.text = \"...\"
	var ip := await _get_ip()
	address_label.text = ip

func _process(dt: float) -> void:
	time += dt
	status_label.text = \"None\"

	if _connection != null:
		match _connection.get_status():
			StreamPeerTCP.STATUS_NONE: status_label.text = \"None\"
			StreamPeerTCP.STATUS_CONNECTING: status_label.text = \"Connecting...\"
			StreamPeerTCP.STATUS_CONNECTED: status_label.text = \"Connected\"
			StreamPeerTCP.STATUS_ERROR: status_label.text = \"Error\"
		
		_connection.poll()

	if _client != null:
		_client.poll()

		if time >= _last_list + list_interval:
			_update_list()
			_last_list = time

func _connect() -> void:
	var input := host_input.text
	var host := input
	var port := 9980

	if input.contains(\":\"):
		var at := input.rfind(\":\")
		host = input.substr(0, at)
		port = int(input.substr(at + 1))

	print(\"Connecting to nohub at %s:%d\" % [host, port])

	_connection = StreamPeerTCP.new()
	_connection.connect_to_host(host, port)

	_client = NohubClient.new(_connection)

func _disconnect() -> void:
	if _connection == null:
		return

	print(\"Disconnecting from host\")
	_connection.disconnect_from_host()
	_connection = null
	_client = null

	lobby_list.clear()
	_lobbies_in_list.clear()

func _create_lobby() -> void:
	var name := create_name_input.text
	var lobby := await _client.create_lobby(\"enet://\" + address_label.text, { \"name\": name })
	if lobby == null:
		popup(\"Failed to create lobby!\")
		return

	print(\"Created lobby #%s\" % lobby.id)
	var data := lobby.data.duplicate()
	data.merge({ \"created-at\": Time.get_datetime_string_from_system() })
	if not await _client.set_lobby_data(lobby.id, data):
		print(\"Failed to set lobby data\")

	create_name_input.text = \"\"

	_update_list()

func _select_lobby(clicked_idx: int) -> void:
	var row_idx := clicked_idx / lobby_list.max_columns - 1
	if row_idx < 0:
		# User clicked header row, do nothing
		return

	var lobby := _lobbies_in_list[row_idx]
	set_selected_lobby(lobby)

func _join_lobby() -> void:
	var lobby := get_selected_lobby()
	if not is_instance_valid(lobby):
		# No lobby selected!
		return

	var address := await _client.join_lobby(lobby.id)

	if not address:
		popup(\"Failed to join lobby #%s!\" % [lobby.id])
	else:
		popup(\"Lobby address: %s\" % [address])

func _lock_lobby():
	var lobby := get_selected_lobby()
	if not is_instance_valid(lobby):
		# No lobby selected!
		return

	if lobby.is_locked:
		if not await _client.unlock_lobby(lobby.id):
			popup(\"Failed to unlock lobby #%s!\" % [lobby.id])
	else:
		if not await _client.lock_lobby(lobby.id):
			popup(\"Failed to lock lobby #%s!\" % [lobby.id])

	_update_list()

func _hide_lobby():
	var lobby := get_selected_lobby()
	if not is_instance_valid(lobby):
		# No lobby selected!
		return

	if lobby.is_visible:
		if not await _client.publish_lobby(lobby.id):
			popup(\"Failed to publish lobby #%s!\" % [lobby.id])
	else:
		if not await _client.hide_lobby(lobby.id):
			popup(\"Failed to hide lobby #%s!\" % [lobby.id])

	_update_list()

func _update_list() -> void:
	if _client == null: return

	var lobbies := await _client.list_lobbies()
	print(\"Received lobbies: %s\" % [lobbies])

	lobby_list.clear()

	# Add header
	if lobby_list.item_count == 0:
		lobby_list.add_item(\"ID\", null, false)
		lobby_list.add_item(\"Name\", null, false)
		lobby_list.add_item(\"Locked?\", null, false)
		lobby_list.add_item(\"Hidden?\", null, false)
		lobby_list.add_item(\"Data\", null, false)

	# Add lobbies that are not in the list yet
	for lobby in lobbies:
		lobby_list.add_item(lobby.id)
		lobby_list.add_item(lobby.data.get(\"name\", \"\"))
		lobby_list.add_item(\"Locked\" if lobby.is_locked else \"Open\")
		lobby_list.add_item(\"Hidden\" if not lobby.is_visible else \"Public\")
		lobby_list.add_item(JSON.stringify(lobby.data))

	_lobbies_in_list.assign(lobbies)

	# Update selected lobby
	if get_selected_lobby() != null:
		var search_id := get_selected_lobby().id
		set_selected_lobby(null)
		for lobby in _lobbies_in_list:
			if lobby.id == search_id:
				set_selected_lobby(lobby)

func _get_ip() -> String:
	var ip := await _get_ip_ipify()
	if not ip: ip = _get_ip_local()

	return ip

func _get_ip_ipify() -> String:
	var request := HTTPRequest.new()
	add_child(request)
	
	if request.request(\"https://api.ipify.org/\") != OK:
		return \"\"

	var response = await request.request_completed
	var result := response[0] as int
	var response_code := response[1] as int
	var headers := response[2] as PackedStringArray
	var body := response[3] as PackedByteArray

	if result != HTTPRequest.RESULT_SUCCESS or response_code != 200:
		return \"\"

	return body.get_string_from_utf8()

func _get_ip_local() -> String:
	return IP.get_local_addresses()[0]
"

[node name="Browser" type="Control"]
layout_mode = 3
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2
script = SubResource("GDScript_xkt7l")

[node name="Browser UI" type="HBoxContainer" parent="."]
layout_mode = 1
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2

[node name="Left Panel" type="PanelContainer" parent="Browser UI"]
layout_mode = 2

[node name="VBoxContainer" type="VBoxContainer" parent="Browser UI/Left Panel"]
layout_mode = 2

[node name="Host Input" type="LineEdit" parent="Browser UI/Left Panel/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2
placeholder_text = "Address"

[node name="Buttons Container" type="HBoxContainer" parent="Browser UI/Left Panel/VBoxContainer"]
layout_mode = 2

[node name="Connect Button" type="Button" parent="Browser UI/Left Panel/VBoxContainer/Buttons Container"]
unique_name_in_owner = true
layout_mode = 2
text = "Connect"

[node name="Disconnect Button" type="Button" parent="Browser UI/Left Panel/VBoxContainer/Buttons Container"]
unique_name_in_owner = true
layout_mode = 2
text = "Disconnect"

[node name="Status Container" type="HBoxContainer" parent="Browser UI/Left Panel/VBoxContainer"]
layout_mode = 2

[node name="Status Prefix" type="Label" parent="Browser UI/Left Panel/VBoxContainer/Status Container"]
layout_mode = 2
text = "Status:"

[node name="Status Label" type="Label" parent="Browser UI/Left Panel/VBoxContainer/Status Container"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
text = "None"

[node name="HSeparator" type="HSeparator" parent="Browser UI/Left Panel/VBoxContainer"]
layout_mode = 2

[node name="Address Prefix" type="Label" parent="Browser UI/Left Panel/VBoxContainer"]
layout_mode = 2
text = "Your address:"

[node name="Address Label" type="Label" parent="Browser UI/Left Panel/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2

[node name="VBoxContainer" type="VBoxContainer" parent="Browser UI"]
layout_mode = 2
size_flags_horizontal = 3

[node name="List Panel" type="ScrollContainer" parent="Browser UI/VBoxContainer"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Lobby List" type="ItemList" parent="Browser UI/VBoxContainer/List Panel"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3
max_text_lines = 4
max_columns = 5
same_column_width = true
fixed_column_width = 160

[node name="Tools" type="HBoxContainer" parent="Browser UI/VBoxContainer"]
layout_mode = 2

[node name="Create Container" type="PanelContainer" parent="Browser UI/VBoxContainer/Tools"]
layout_mode = 2
size_flags_horizontal = 3

[node name="VBoxContainer" type="VBoxContainer" parent="Browser UI/VBoxContainer/Tools/Create Container"]
layout_mode = 2

[node name="Lobby Name Label" type="Label" parent="Browser UI/VBoxContainer/Tools/Create Container/VBoxContainer"]
layout_mode = 2
text = "Lobby name: "

[node name="Lobby Name Input" type="LineEdit" parent="Browser UI/VBoxContainer/Tools/Create Container/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3

[node name="Create Lobby Button" type="Button" parent="Browser UI/VBoxContainer/Tools/Create Container/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 4
text = "Create Lobby"

[node name="Tools Container" type="PanelContainer" parent="Browser UI/VBoxContainer/Tools"]
layout_mode = 2
size_flags_horizontal = 3

[node name="Rows" type="VBoxContainer" parent="Browser UI/VBoxContainer/Tools/Tools Container"]
layout_mode = 2

[node name="Selected Lobby Row" type="HBoxContainer" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows"]
layout_mode = 2

[node name="Selected Lobby Prefix" type="Label" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows/Selected Lobby Row"]
layout_mode = 2
text = "Selected Lobby: "

[node name="Selected Lobby Label" type="Label" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows/Selected Lobby Row"]
unique_name_in_owner = true
layout_mode = 2

[node name="Tools Row" type="HBoxContainer" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows"]
layout_mode = 2
size_flags_vertical = 10

[node name="Lock Lobby Button" type="Button" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows/Tools Row"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
text = "Lock"

[node name="Hide Lobby Button" type="Button" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows/Tools Row"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
text = "Hide"

[node name="Join Lobby Button" type="Button" parent="Browser UI/VBoxContainer/Tools/Tools Container/Rows/Tools Row"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
text = "Join"

[node name="Modal" type="PopupPanel" parent="."]
unique_name_in_owner = true
initial_position = 4
size = Vector2i(248, 168)

[node name="PanelContainer" type="PanelContainer" parent="Modal"]
custom_minimum_size = Vector2(240, 160)
offset_left = 4.0
offset_top = 4.0
offset_right = 244.0
offset_bottom = 164.0

[node name="VBoxContainer" type="VBoxContainer" parent="Modal/PanelContainer"]
layout_mode = 2

[node name="Message Label" type="Label" parent="Modal/PanelContainer/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2
size_flags_vertical = 3
text = "Message here"

[node name="Confirm Button" type="Button" parent="Modal/PanelContainer/VBoxContainer"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 4
text = "OK"
